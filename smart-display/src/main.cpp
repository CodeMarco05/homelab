/*
 * Ported LVGL 8.4 and display the official demo interface.
 */
#include <Arduino.h>
#include "rgb_lcd_port.h" // Header for Waveshare RGB LCD driver
#include "gt911.h"        // Header for touch screen operations (GT911)
#include "lvgl_port.h"      // LVGL porting functions for integration
// #include <demos/lv_demos.h> // LVGL demo headers
#include "ui.h"             // UI header generated by SquareLine Studio
#include <WiFi.h>
#include <HTTPClient.h>


// https://www.youtube.com/nishad2m8
// https://buymeacoffee.com/nishad2m8

static const char *TAG = "main";  // Tag for logging

// WiFi credentials
const char* ssid = "BrHomeLan";
const char* password = "20BrHomeSafe20";

extern objects_t objects;

unsigned long unixTime = 0;

unsigned long getUnixTime() {
  if (WiFi.status() != WL_CONNECTED) {
    return 0;
  }
  
  HTTPClient http;
  http.begin("https://digidates.de/api/v1/unixtime");
  
  int httpCode = http.GET();
  
  if (httpCode != HTTP_CODE_OK) {
    http.end();
    return 0;
  }
  
  String payload = http.getString();
  http.end();
  
  // JSON manuell parsen: {"time": 1766526829}
  int startPos = payload.indexOf(":") + 1;
  int endPos = payload.indexOf("}");
  String timeStr = payload.substring(startPos, endPos);
  timeStr.trim();
  
  return timeStr.toInt();
}

String formatUnixTime(unsigned long unixTime) {
  if (unixTime == 0) {
    return "UngÃ¼ltige Zeit";
  }
  
  // Unix timestamp in Datum/Zeit umwandeln
  time_t rawTime = (time_t)unixTime;
  struct tm* timeInfo = gmtime(&rawTime);
  
  // Wochentage auf Deutsch
  const char* wochentage[] = {"So", "Mo", "Di", "Mi", "Do", "Fr", "Sa"};
  
  // Format: Mo 12.03.2025 12:23:45
  char buffer[30];
  snprintf(buffer, sizeof(buffer), "%s %02d.%02d.%04d %02d:%02d:%02d",
           wochentage[timeInfo->tm_wday],
           timeInfo->tm_mday,
           timeInfo->tm_mon + 1,
           timeInfo->tm_year + 1900,
           timeInfo->tm_hour,
           timeInfo->tm_min,
           timeInfo->tm_sec);
  
  return String(buffer);
}


void connectWiFi() {
  Serial.print("Connecting to WiFi");
  WiFi.begin(ssid, password);
  
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  
  Serial.println("\nWiFi connected!");
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());
  
  // Update UI with IP address
  /*if (lvgl_port_lock(-1)) {
    char buffer[64];
    snprintf(buffer, sizeof(buffer), "IP: %s", WiFi.localIP().toString().c_str());
    lv_label_set_text(objects.header, buffer);
    lvgl_port_unlock();
  }*/
  
  // Fetch initial unix time
  unixTime = getUnixTime();
  if (unixTime > 0) {
    Serial.print("Initial time: ");
    Serial.println(formatUnixTime(unixTime));
  }
}


void setup()
{
  Serial.begin(115200);
  delay(1000);
  Serial.println("LVGL Demo Start");
  static esp_lcd_panel_handle_t panel_handle = NULL; // Declare a handle for the LCD panel
  static esp_lcd_touch_handle_t tp_handle = NULL;    // Declare a handle for the touch panel

  // Initialize the GT911 touch screen controller
  tp_handle = touch_gt911_init();


  // Initialize the Waveshare ESP32-S3 RGB LCD hardware
  panel_handle = waveshare_esp32_s3_rgb_lcd_init();

  // Turn on the LCD backlight
  //wavesahre_rgb_lcd_bl_on();


  // Initialize LVGL with the panel and touch handles
  ESP_ERROR_CHECK(lvgl_port_init(panel_handle, tp_handle));

  ESP_LOGI(TAG, "Display LVGL demos");

  // Lock the mutex because LVGL APIs are not thread-safe
  if (lvgl_port_lock(-1))
  {
    // Uncomment and run the desired demo functions here
    // lv_demo_stress();  // Stress test demo
    // lv_demo_benchmark(); // Benchmark demo
    // lv_demo_music();     // Music demo
    // lv_demo_widgets();    // Widgets demo
    ui_init();
    // Release the mutex after the demo execution
    lvgl_port_unlock();
  }
  connectWiFi();

  // wifi_sta_init("valorant", "88888888");
}

void loop()
{
  static int counter = 1;
  static unsigned long lastUpdate = 0;
  static unsigned long lastFetch = 0;

  
  // Update every 1000ms (1 second)
  if (millis() - lastUpdate >= 1000) {
    lastUpdate = millis();
    
    // Just print to serial
    Serial.print("Hello World: ");
    Serial.println(counter);

    // Increment unix time by 1 second
    if (unixTime > 0) {
      unixTime++;
    }
    
    // Update date label every second
    if (lvgl_port_lock(-1)) {
      String formattedTime = formatUnixTime(unixTime);
      lv_label_set_text(objects.date, formattedTime.c_str());
      lvgl_port_unlock();
    }

    counter++;
  }
  
  // Fetch fresh unix time every 60 seconds
  if (millis() - lastFetch >= 60000) {
    lastFetch = millis();
    
    unsigned long newTime = getUnixTime();
    if (newTime > 0) {
      unixTime = newTime;
      Serial.println("Time synchronized from server");
    }
  }
}
